task copyPackageXML(type: Sync) {
    def installDistTask = project(':org.spf.packagexml.dsl.ide').tasks.installDist
    dependsOn installDistTask
    from installDistTask.outputs
    into 'src/packagexml'
}

task copyCMakeLists(type: Sync) {
    def installDistTask = project(':org.spf.cmakelists.dsl.ide').tasks.installDist
    dependsOn installDistTask
    from installDistTask.outputs
    into 'src/cmakelists'
}

clean {
    doLast {
        delete copyPackageXML.outputs
        delete copyCMakeLists.outputs
    }
}

npmInstall.dependsOn copyPackageXML
npmInstall.dependsOn copyCMakeLists

task installExtension(type: Exec, dependsOn: vscodeExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args '--install-extension', vscodeExtension.destPath
}

task startCode(type:Exec, dependsOn: installExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args "$rootProject.projectDir/trial/", '--new-window'
}

task publish(dependsOn: vscodeExtension, type: NodeTask) {
    script = file("$rootProject.projectDir/node_modules/vsce/out/vsce")
    args = [ 'publish', '-p', System.getenv('ACCESS_TOKEN'), "--packagePath", "${project.buildDir}/vscode/vscode-extension-self-contained-${project.version}.vsix"]
    execOverrides {
        workingDir = projectDir
    }
}